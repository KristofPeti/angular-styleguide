# SPDX-FileCopyrightText: 2022 Carl Zeiss AG
#
# SPDX-License-Identifier: MIT

# app-product-master spec

@objects
  header        app-product-master header
    title       h1
    subtitle    h2
  content       app-product-master .grid-container
    product-*   app-product

@set
  titleFontSize 65px
  subtitleFontSize 35px

== Header ==

  header:
    width 100 % of screen/width

  @on desktop
    header:
      height ~ 279 px

  @on mobile
    header:
      height 350 to 400 px

  header.title:
    centered horizontally inside header
    css font-size is "${titleFontSize}"

  header.subtitle:
    centered horizontally inside header
    css font-size is "${subtitleFontSize}"
    below header.title

== Content ==

  content:
    centered horizontally inside viewport

  @on desktop
    content:
      width ~ 58 % of viewport/width

    @forEach [content.product-*] as product
      ${product}:
        height 220 to 290 px
        width 130 to 190 px
        component components/product.component.gspec

    @for [1 - 2] as index
      content.product-${index}:
        aligned horizontally all content.product-3

    @for [4 - 5] as index
      content.product-${index}:
        aligned horizontally all content.product-6

  @on mobile
    content:
      width ~ 77 % of viewport/width

    @forEach [content.product-*] as product
      ${product}:
        height 230 to 300 px
        width 130 to 200 px
        component components/product.component.gspec

    @for [1 - 5] as index
      content.product-${index}:
        aligned vertically all content.product-6
